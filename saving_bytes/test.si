var memory      = 0
var next_memory = 0

def on_reset($scratch_space: [Int], test: Int) {

    .memory = 0
    .next_memory = 0

}

def get_input($scratch_space: [Int], tick: Int, $inputs: [Int]) {

    .memory = .next_memory
    inputs[3] = .memory
    var action = random(5)
    var num    = random(255)

    if tick == 0 {
        action = 2
    }

    var read = 0
    var save = 0

    if action == 1 {

        read = 1

    } elif action == 2 {

        save = 1
        .next_memory = num

    } elif action == 3 {

        read = 1
        save = 1
        .next_memory = num

    }

    inputs[0] = read
    inputs[1] = save
    inputs[2] = num

}

def check_output($scratch_space: [Int], tick: Int, inputs: [Int], outputs: [Int]) TestResult {

    if inputs[0] != outputs[1] {
        if inputs[0] == 1 {
            set_error("Output enable should be ON.")
            return fail
        } else {
            set_error("Output enable should be OFF.")
            return fail
        }
    }

    if inputs[0] == 1 && outputs[0] != .memory {
        set_error(`Expected output {.memory}.`)
        return fail
    }

    if tick == 64 {
        return win
    }

}